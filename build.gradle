plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
    id 'pro.mikey.plugins.insaniam' version "0.1-SNAPSHOT"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    publishing {
        repositories {
            if (providers.environmentVariable("NANITE_DEPLOY").isPresent()) {
                maven {
                    url "https://maven.nanite.dev/${providers.environmentVariable("SNAPSHOT").map { "snapshots" }.getOrElse("releases")}"
                    credentials {
                        username = "nanite"
                        password = "${providers.environmentVariable("NANITE_DEPLOY").get()}"
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}

def changelogData = insaniamUtils.createChangelog {
    file = file('CHANGELOG.md')
    versionPattern = ~/## \[[^]]+]/
    fallbackValue = "No changelog provided"
    version = project.mod_version
}

publishMods {
    dryRun = providers.environmentVariable("CURSE_TOKEN").getOrNull() == null
    changelog = changelogData.replace("# Just Hammers Changelog", "").trim()
    version = "${mod_version}"
    type = STABLE

    def fabricOptions = publishOptions {
        file = project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[FABRIC] Just Hammers ${mod_version}"
        modLoaders.add("fabric")
    }

    def neoForge = publishOptions {
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[NEOFORGE] Just Hammers ${mod_version}"
        modLoaders.add("neoforge")
    }

    def curseForgeOptions = curseforgeOptions {
        accessToken = providers.environmentVariable("CURSE_TOKEN")
        projectId = "${curseforge_id}"
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_21)
    }

    def modrinthOptions = modrinthOptions {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "${modrinth_id}"
        minecraftVersions.add("${minecraft_version}")
    }

    curseforge("curseforgeFabric") {
        from(curseForgeOptions, fabricOptions)
        requires("architectury-api")
        requires("fabric-api")
    }

    curseforge("curseforgeNeoForge") {
        from(curseForgeOptions, neoForge)
        requires("architectury-api")
    }

    modrinth("modrinthFabric") {
        from(modrinthOptions, fabricOptions)
        requires("architectury-api")
        requires("fabric-api")
    }

    modrinth("modrinthNeoforge") {
        from(modrinthOptions, neoForge)
        requires("architectury-api")
    }

    github {
//        file = project.provider { project(":forge").tasks.remapJar }.flatMap { it.archiveFile }
        additionalFiles.from project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }

        announcementTitle = "[${minecraft_version}] Just Hammers ${mod_version}"

        repository = "${project.github_repo}"
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        commitish = providers.environmentVariable("GITHUB_SHA").orElse("dryRun")
        tagName = providers.environmentVariable("GITHUB_REF_NAME").orElse("dryRun")
    }
}

