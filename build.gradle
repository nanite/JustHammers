plugins {
    id 'java-library'
    id 'fabric-loom' version '1.12-SNAPSHOT' apply(false)
    id 'net.neoforged.moddev' version '2.0.112' apply(false)
    id "me.modmuss50.mod-publish-plugin" version "1.0.0"
    id 'pro.mikey.plugins.insaniam' version "0.1-SNAPSHOT"
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = 'pro.mikey.mods'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    version = mod_version

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 21
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    publishing {
        repositories {
            if (providers.environmentVariable("NANITE_DEPLOY").isPresent()) {
                maven {
                    url "https://maven.nanite.dev/${providers.environmentVariable("SNAPSHOT").map { "snapshots" }.getOrElse("releases")}"
                    credentials {
                        username = "nanite"
                        password = "${providers.environmentVariable("NANITE_DEPLOY").get()}"
                    }
                }
            }
        }
    }
}

subprojects {
    base {
        archivesName = "just-hammers-$project.name"
    }

    repositories {
        maven {
            url "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
    }

    if (project.name != "common") {
        dependencies {
            api project(":common")
        }

        jar {
            from project(":common").sourceSets.main.output
        }
    }

    def replaceProperties = [
            mod_id: mod_id,
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range.replace("%base%", minecraft_version),
            neoforge_version: neoforge_version,
            neoforge_version_range: neoforge_version_range,
            fabric_loader_version: fabric_loader_version,
            fabric_api_version: fabric_api_version,
            version: version,
    ]

    processResources {
        inputs.properties replaceProperties

        filesMatching(["META-INF/neoforge.mods.toml", "fabric.mod.json", "META-INF/mods.toml"]) {
            expand replaceProperties
        }
    }
}

def changelogData = insaniamUtils.createChangelog {
    file = file('CHANGELOG.md')
    versionPattern = ~/## \[[^]]+]/
    fallbackValue = "No changelog provided"
    version = project.mod_version
}

publishMods {
    dryRun = providers.environmentVariable("CURSE_TOKEN").getOrNull() == null
    changelog = changelogData.replace("# Just Hammers Changelog", "").trim()
    version = "${mod_version}"
    type = STABLE

    def fabricOptions = publishOptions {
        file = project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[FABRIC] Just Hammers ${mod_version}"
        modLoaders.add("fabric")
    }

    def neoForge = publishOptions {
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }
        displayName = "[NEOFORGE] Just Hammers ${mod_version}"
        modLoaders.add("neoforge")
    }

    def curseForgeOptions = curseforgeOptions {
        accessToken = providers.environmentVariable("CURSE_TOKEN")
        projectId = "${curseforge_id}"
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_21)
    }

    def modrinthOptions = modrinthOptions {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "${modrinth_id}"
        minecraftVersions.add("${minecraft_version}")
    }

    curseforge("curseforgeFabric") {
        from(curseForgeOptions, fabricOptions)
        requires("fabric-api")
    }

    curseforge("curseforgeNeoForge") {
        from(curseForgeOptions, neoForge)
    }

    modrinth("modrinthFabric") {
        from(modrinthOptions, fabricOptions)
        requires("fabric-api")
    }

    modrinth("modrinthNeoforge") {
        from(modrinthOptions, neoForge)
    }

    github {
//        file = project.provider { project(":forge").tasks.remapJar }.flatMap { it.archiveFile }
        additionalFiles.from project.provider { project(":fabric").tasks.remapJar }.flatMap { it.archiveFile }
        file = project.provider { project(":neoforge").tasks.remapJar }.flatMap { it.archiveFile }

        announcementTitle = "[${minecraft_version}] Just Hammers ${mod_version}"

        repository = "${project.github_repo}"
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        commitish = providers.environmentVariable("GITHUB_SHA").orElse("dryRun")
        tagName = providers.environmentVariable("GITHUB_REF_NAME").orElse("dryRun")
    }
}

